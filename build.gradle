plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group 'com.github.MarceloFRSilva'
version '1.0'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addBooleanOption('Xdoclint:none', true)
        }
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: username, password: password)
            }

            pom.project {
                name 'KademliaDHT_BSS'
                packaging 'jar'
                description 'KademliaDHT implementation in Java'
                url 'https://github.com/MarceloFRSilva/KademliaDHT_BSS'

                scm {
                    connection 'scm:git:git://github.com/MarceloFRSilva/KademliaDHT_BSS.git'
                    developerConnection 'scm:git:git@github.com:MarceloFRSilva/KademliaDHT_BSS.git'
                    url 'https://github.com/MarceloFRSilva/KademliaDHT_BSS'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                        distribution repository
                    }
                }

                developers {
                    developer {
                        id 'JoshuaKissoon'
                        name 'Joshua Kissoon'
                        email 'JoshuaKissoon@gmail.com'
                    }
                    developer {
                        id 'ChronosX88'
                        name 'ChronosX88'
                        email 'chronosx88@gmail.com'
                    }
                }
            }
        }
    }
}


sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
}
